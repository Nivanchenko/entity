#Использовать decorator

Функция ТипСущности(Сущность) Экспорт
	ТипСущности = ТипЗнч(Сущность);
	Если ТипСущности = Тип("Сценарий") Тогда
		ТипСущности = ОбработкаДекоратора.ИсходныйТип(Сущность);
	КонецЕсли;

	Возврат ТипСущности;
КонецФункции

Функция СоздатьИзМенеджера(ОбъектМодели, МенеджерСущностей) Экспорт
	Сущность = Новый(ОбъектМодели.ТипСущности());

	Декоратор = Новый ПостроительДекоратора(Сущность)
			.Импорт(Новый Импорт("decorator"))

			.Поле(Новый Поле("_МенеджерСущностей")
					.ЗначениеПоУмолчанию(МенеджерСущностей))

			.Поле(Новый Поле("_ОбъектМодели")
					.ЗначениеПоУмолчанию(ОбъектМодели))

			.Метод(Новый Метод("Прочитать")
						.Публичный()
						.ТелоМетода("_ТипСущности = ОбработкаДекоратора.ИсходныйТип(ЭтотОбъект);
									|_ДанныеСущности = _МенеджерСущностей.ПолучитьОдно(
									|	_ТипСущности,
									|	_ОбъектМодели.ПолучитьЗначениеИдентификатора(ЭтотОбъект)
									|);
									|ОбработкаДекоратора.СинхронизироватьПоля(_ДанныеСущности, ЭтотОбъект);"))


			.Метод(Новый Метод("Сохранить")
						.Публичный()
						.ТелоМетода("_МенеджерСущностей.Сохранить(ЭтотОбъект);"))

			.Метод(Новый Метод("Удалить")
						.Публичный()
						.ТелоМетода("_МенеджерСущностей.Удалить(ЭтотОбъект);"))

			.Построить();

	Возврат Декоратор;
КонецФункции

Функция СоздатьИзХранилища(ОбъектМодели, ХранилищеСущностей) Экспорт
	Сущность = Новый(ОбъектМодели.ТипСущности());

	Декоратор = Новый ПостроительДекоратора(Сущность)
		.Импорт(Новый Импорт("decorator"))

		.Поле(Новый Поле("_ХранилищеСущностей")
				.ЗначениеПоУмолчанию(ХранилищеСущностей))

		.Поле(Новый Поле("_ОбъектМодели")
				.ЗначениеПоУмолчанию(ОбъектМодели))

		.Метод(Новый Метод("Прочитать")
					.Публичный()
					.ТелоМетода("_ДанныеСущности = _ХранилищеСущностей.ПолучитьОдно(_ОбъектМодели.ПолучитьЗначениеИдентификатора(ЭтотОбъект));
								|ОбработкаДекоратора.СинхронизироватьПоля(_ДанныеСущности, ЭтотОбъект);"))


		.Метод(Новый Метод("Сохранить")
					.Публичный()
					.ТелоМетода("_ХранилищеСущностей.Сохранить(ЭтотОбъект);"))

		.Метод(Новый Метод("Удалить")
					.Публичный()
					.ТелоМетода("_ХранилищеСущностей.Удалить(ЭтотОбъект);"))

		.Построить();
	
	Возврат Декоратор;
КонецФункции